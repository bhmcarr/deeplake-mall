// room script file

// initial room setup
function room_Load()
{ 
  // play broken tv animation on loop
  oTVStatic.SetView(21);
  oTVStatic.Animate(0, 1, eRepeat, eNoBlock);
  
  // sleeping Z's animation
  oSleepyZs.Visible = false;
  oSleepyZs.SetView(24);
  oSleepyZs.Animate(0, 4, eRepeat, eNoBlock);
  
  // opening scene
  if (Room.GetProperty("openingScene") == false){
    cTrenton.x = 105;
    cTrenton.y = 137;
    cTrenton.FaceDirection(eDirectionRight);
    cTrenton.ChangeView(41);
  }
  
  if (cTrenton.GetProperty("closingTime") == true){
    oKeys.Visible = true;
  }
  
  aSecurityroom2.Play();
  
}

function room_AfterFadeIn()
{
  // opening scene
  if (Room.GetProperty("openingScene") == false){
    cTrenton.SpeechView = 23;
    dBillOpening.Start();
    Room.SetProperty("openingScene", 1);
  }
  
  // closing time scene
  if (cTrenton.GetProperty("closingTime") == true){
    dBillClosing.Start();
  }
}

function repeatedly_execute_always()
{
  if (cBill.View == 25){
    oSleepyZs.Visible = true;
  }
}

function stickyNote1_Look()
{
  player.Say("It says, 'Remember to buy milk on the way home!'");
  player.Say("Why is this here?");
}

function poster1_Look()
{
  player.Say("Some manager slapped this thing up years ago.");
  player.Say("Funny, I feel anything but inspired right now.");
}

function stickyNote2_Look()
{
   player.Say("It says, 'Note to self, buy more sticky notes.'");
}

function officeComputer_Look()
{
  player.Say("It's an old eyeBac computer. Still works, somehow.");
}

// busted screen
function oTVStatic_Look()
{
  player.Say("It's got a huge crack in it..");
}

function oTVStatic_Interact()
{
  player.Walk(oTVStatic.X, oTVStatic.Y, eBlock);
  billScreen1.Start();
}

function officeComputer_Interact()
{
  player.Walk(189, 132, eBlock);
  cTrenton.FaceDirection(eDirectionLeft);
  if (player.GetProperty("knowsDoogle") == 1){
    oDoogleScreen.Visible = true;
    dDoogleDialog1.Start();
  }
  else {
    player.Say("I probably shouldn't be playing card games at work..");
  }
  
}

// office door interactions (clean this up holy crap)
function officeDoor_Interact()
{
  player.Walk(290, 139, eBlock);
  player.ChangeRoom(2, 25, 135);
}
function officeDoor_AnyClick()
{
  player.Walk(290, 139, eBlock);
  player.ChangeRoom(2, 25, 135);
}

function hVapeShotScreen_Look()
{
  if (cBill.View != 25 && cTrenton.GetProperty("stopChet") == false){
    player.Say("Is that the vape shop?");
    cBill.Say("Yeah, there's always hooligans goin' in that store.");
    cBill.Say("I'm gonna catch 'em doing something someday!");
  }
  else if (cTrenton.GetProperty("stopChet") == true){
    player.Walk(70, 128, eBlock);
    player.Say("Let's check the footage..");
    player.Say("*click*");
    player.Say("I knew it! I can see those bozos making the sign!");
    player.Say("This ends now!");
    cTrenton.SetProperty("sawFootage", 1);
  }
}

function oKeys_AnyClick()
{
  player.Walk(oKeys.X, oKeys.Y, eBlock);
  if (cBill.View != 25){
    cBill.Say("Hey! Hands off, pal!");
    player.Say("Uh.. sorry.");
  }
  else {
    player.Say("Yoink!");
    oKeys.Visible = false;
    player.AddInventory(iKeyMadeWithLove);
    // turn off state to stop stuff from happening at inappropriate times
    cTrenton.SetProperty("closingTime", 0);
  }
}
